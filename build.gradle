buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "net.saliman:gradle-cobertura-plugin:1.1.2"
    }
}

apply plugin: 'cobertura'
apply plugin: 'groovy'

repositories { 
  mavenCentral()
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

group = 'com.jacobo.gradle'

sourceSets {
  integTest {
     compileClasspath += main.output + test.output
     runtimeClasspath += main.output + test.output
    groovy {
      srcDir 'src/integTest/groovy'
    }
  }
}

configurations { 
  integTestCompile.extendsFrom testCompile
  integTestRuntime.extendsFrom testRuntime
}

dependencies {
  compile gradleApi()
  groovy localGroovy()
  testCompile "org.spockframework:spock-core:0.6-groovy-1.8" 
}

cobertura {
    coverageSourceDirs << project.sourceSets.main.groovy.srcDirs
}

task integTest(type: Test, dependsOn: 'test') {
  testClassesDir = sourceSets.integTest.output.classesDir
  classpath = sourceSets.integTest.runtimeClasspath
}

check.dependsOn(integTest)

task('unversionedJar', type: Jar, dependsOn: 'jar') {
  version = null
  from sourceSets.main.output
}

task('installPlugin', type: Copy, dependsOn: 'unversionedJar') {
  def gradleInstallDir = gradle.gradleHomeDir
  group = 'installation'
  description = 'Installs the plugin jar in your gradle distribution.'
  from "${buildDir}/libs/${unversionedJar.archiveName}"
  into "/${gradleInstallDir}/lib/plugins"
}

installPlugin.doLast {
  def gradleInstallDir = gradle.gradleHomeDir
  println "Installed in: ${gradleInstallDir}/lib/plugins as: ${unversionedJar.archiveName}"
}

task('uninstallPlugin', type: Delete) {
  def gradleInstallDir = gradle.gradleHomeDir
  group = 'installation'
  description = 'Removes the plugin from your gradle distribution.'
  delete("/${gradleInstallDir}/lib/plugins/${unversionedJar.archiveName}")
}

uploadArchives {
  repositories {
    ivy {
      url = bintray_api_base_url + '/content/' + bintray_username + '/' + bintray_repo + '/' + bintray_package + '/' + version
      credentials {
	username bintray_username
	password bintray_api_key
      }
    }
  }
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.4'
}